import com.commercehub.gradle.plugin.avro.GenerateAvroJavaTask

buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        //plugin needed to generate POJO from Avro schema
        classpath("com.commercehub.gradle.plugin:gradle-avro-plugin:0.14.2")
    }
}
plugins {
    id 'org.springframework.boot' version '2.3.1.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'java'
}

group = 'one.digitalinnovation.checkout'
version = '0.0.1-SNAPSHOT'

sourceCompatibility = 11

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
    maven {
        url 'https://packages.confluent.io/maven/'
    }
}

ext {
    set('springCloudVersion', "Hoxton.SR6")
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web:2.5.3'
    implementation 'org.springframework.cloud:spring-cloud-starter-sleuth'
    implementation 'org.springframework.cloud:spring-cloud-starter-stream-kafka:3.1.3'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa:2.5.4'
    implementation 'io.confluent:kafka-avro-serializer:5.5.0'

    compileOnly 'org.projectlombok:lombok:1.18.20'
    runtimeOnly 'org.postgresql:postgresql:42.2.23.jre7'
    annotationProcessor 'org.projectlombok:lombok:1.18.20'
    testImplementation('org.springframework.boot:spring-boot-starter-test:2.5.3') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation 'org.springframework.cloud:spring-cloud-stream-test-support:3.1.3'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

test {
    useJUnitPlatform()
}

task generateAvro(type: GenerateAvroJavaTask) {
    //point plugin to folder with avro schemas
    source("./src/main/resources/avro")
    //point plugin to folder with generated POJOs
    outputDir = file("./src/main/java")
}

compileJava.source(generateAvro.outputs)